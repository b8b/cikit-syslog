buildscript {
    ext.kotlin_version = "1.3.0"

    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3"
        classpath "me.champeau.gradle:jmh-gradle-plugin:0.4.7"
    }
}

group = "org.cikit.syslog"
version = "CURRENT"

apply plugin: "java"
apply plugin: "kotlin"
apply plugin: "maven-publish"
apply plugin: "com.jfrog.bintray"
apply plugin: "me.champeau.gradle.jmh"

sourceCompatibility = 1.8

repositories {
    jcenter()
    maven {
        url "https://dl.bintray.com/kotlin/kotlinx"
    }
}

jmh {
    jmhVersion '1.21'
    jvmArgs = ['-Djmh.separateClasspathJAR=true']
    jmhJar {
        archiveName = 'benchmarks.jar'
    }
}

dependencies {
    compile("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
    compile("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.0.0")
    testCompile("junit:junit:4.12")
    testCompile("de.erichseifert.vectorgraphics2d:VectorGraphics2D:0.13")
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

jar {
    dependsOn "generatePomFileForMavenJavaPublication"
    into("META-INF/maven/$project.group/$project.archivesBaseName") {
        from new File(project.buildDir, "publications/mavenJava")
        rename ".*", "pom.xml"
    }
    from sourceSets.main.output
    manifest {
        attributes["Implementation-Title"] = project.name
        attributes["Implementation-Version"] = version
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ["mavenJava"]
    pkg {
        repo = 'cikit'
        name = 'cikit-syslog'
        userOrg = user
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/b8b/cikit-syslog.git'
        labels = ['kotlin']
        publicDownloadNumbers = true
        version {
            name = project.version
            desc = 'rfc5424 syslog implementation'
            vcsTag = "v${project.version}"
        }
    }
}

